#! /usr/bin/env nqp

use NQPHLL;

grammar PHQ::Grammar is HLL::Grammar {
    token TOP {
        :my $*CUR-BLOCK := QAST::Block.new: QAST::Stmts.new;
        ^
        <.shəbang>?
        <statements>
        [ $ || <.panic('Syntax error')> ]
    }

    token shəbang { '#!' \N* \n }

    rule statements {
        ''
        [ <statement> <.eat-terminator> ]*
    }

    token eat-terminator {
        || ';'
        # No semi-colon needed at end of a block
        || <?MARKED('endstmt')>
        # Or if this is the end of file
        || $
    }


    # Statements

    proto token statement {*}

    token statement:sym<null> { '' <?[;]> }

    rule statement:sym<echo> {
        <sym> [ <?[(]> '(' <EXPR> ')' || <EXPR> ]
    }

    token statement:sym<EXPR> { <EXPR> }

    rule statement:sym<function> {
        <sym>
        <funcname>
        <funcbody>
    }

    rule funcbody {
        :my $*CUR-BLOCK := QAST::Block.new: QAST::Stmts.new;
        '(' <param>* % ',' ')'
        <block>
    }

    token param { <varname> }

    rule statement:sym<if> {
        <sym>      '(' <EXPR> ')' <block>
        [ 'elseif' '(' <EXPR> ')' <block> ]*
        [ 'else'                  <else=.block> ]?
    }

    rule statement:sym<while> {
        <sym> '(' <EXPR> ')' <block>
    }

    token block {
        # NB: Ensure no <.ws> is called between '}' and <?ENDSTMT>.
        # It needs the newline for <.eol>
        '{' <statements> '}' <?ENDSTMT>
    }

    token ENDSTMT {
        [
        <.eol> <.ws> <?MARKER('endstmt')>
        ]?
    }


    # Literals

    proto token value {*}
    token value:sym<string>  { <?["]> <quote_EXPR: ':q', ':b'> }
    token value:sym<integer> { '-'? \d+ }
    token value:sym<number>  { '-'? \d+ '.' \d+ }


    # EXPR syms

    token term:sym<value> { <value> }

    rule term:sym<call> { <funcname> '(' <EXPR>* % ',' ')'}

    rule term:sym<var> {
        :my $*MAYBE-DECL := 0;
        <varname>
        [ <?before '='<![=]> { $*MAYBE-DECL := 1 }> || <?> ]
    }

    rule term:sym<parenthesised> { '(' ~ ')' <EXPR> }

    token varname { '$' <identifier> }
    token funcname { <!keyword> <.identifier> }
    token identifier { <[_ A..Z a..z]> <[_ A..Z a..z 0..9]>* }

    # Operators
    my %multiplicative := nqp::hash('prec', 'u=', 'assoc', 'left');
    my %additive       := nqp::hash('prec', 't=', 'assoc', 'left');
    my %concatenation  := nqp::hash('prec', 'r=', 'assoc', 'left');
    my %relational     := nqp::hash('prec', 'm=', 'assoc', 'non');
    my %assignment     := nqp::hash('prec', 'i=', 'assoc', 'right');

    token infix:sym<*>    { <sym>  <O(|%multiplicative, :op<mul_n>)> }
    token infix:sym</>    { <sym>  <O(|%multiplicative, :op<div_n>)> }
    token infix:sym<%>    { <sym>  <O(|%multiplicative, :op<mod_n>)> }

    token infix:sym<+>    { <sym>  <O(|%additive,       :op<add_n>)> }
    token infix:sym<->    { <sym>  <O(|%additive,       :op<sub_n>)> }

    # PHP appears to not have a separate level for concatenation;
    # e.g., this fails in PHP: echo "ok " . $foo + $bar;
    token infix:sym<.>    { <sym>  <O(|%additive,  :op<concat>)> }

    token infix:sym«==»   { <sym>  <O(|%relational, :op<iseq_n>)> }
    token infix:sym«!=»   { <sym>  <O(|%relational, :op<isne_n>)> }
    token infix:sym«<=»   { <sym>  <O(|%relational, :op<isle_n>)> }
    token infix:sym«>=»   { <sym>  <O(|%relational, :op<isge_n>)> }
    token infix:sym«<»    { <sym>  <O(|%relational, :op<islt_n>)> }
    token infix:sym«>»    { <sym>  <O(|%relational, :op<isgt_n>)> }

    token infix:sym<=>    { <sym>  <O(|%assignment,     :op<bind>)> }


    # Misc

    token ws { <!ww> <.white>* }
    token white { \s+ || <.comment> }
    token comment { '//' \N* \n }
    token eol { \h* [ \n | <.comment> ] }
    token keyword {
        [
        | if | elseif | else | while | for
        | echo
        ]
        <!ww>
    }
}

class PHQ::Actions is HLL::Actions {
    method TOP($/) {
        $*CUR-BLOCK.push: $<statements>.ast;
        make $*CUR-BLOCK;
    }

    method statements($/) {
        my $stmts := QAST::Stmts.new: :node($/);
        for $<statement> {
            $stmts.push($_.ast);
        }
        make $stmts;
    }


    method statement:sym<null>($/) { make QAST::Op.new: :node($/), :op<null> }

    method statement:sym<function>($/) {
        my $name := ~$<funcname>;
        my $install := $<funcbody>.ast;
        $install.name($name);
        $*CUR-BLOCK[0].push: QAST::Op.new:
            :op<bind>,
            QAST::Var.new(:$name, :scope<lexical>, :decl<var>),
            $install;
        make QAST::Op.new: :node($/), :op<null>;
    }

    method funcbody($/) {
        $*CUR-BLOCK.node($/);
        $*CUR-BLOCK.push($<block>.ast);
        make $*CUR-BLOCK;
    }

    method block($/) { make $<statements>.ast; }

    method param($/) {
        $*CUR-BLOCK[0].push: QAST::Var.new:
            :node($/), :name(~$<varname>), :scope<lexical>, :decl<param>;
        $*CUR-BLOCK.symbol(~$<varname>, :declared);
    }

    method statement:sym<if>($/) {
        # Note: In PHP, blocks don't have their own scope; variables in a
        # block seem to just get installed at global/function scope

        nqp::die("Impossible: mismatched if/elseif tests and blocks")
            unless +$<EXPR> == +$<block>;

        sub make-if($n) {
            my $if := QAST::Op.new:
                    :op<if>,
                    $<EXPR>[$n].ast,
                    $<block>[$n].ast
                    ;

            if ($n + 1 < +$<EXPR>) {
                $if.push: make-if($n + 1);
            }
            elsif ($<else>) {
                $if.push: $<else>.ast;
            }

            $if;
        }

        make make-if(0);
    }

    method statement:sym<while>($/) {
        # Again, no new scope for while in PHP
        make QAST::Op.new:
            :node($/),
            :op<while>,
            $<EXPR>.ast,
            $<block>.ast
    }

    method statement:sym<EXPR>($/) { make $<EXPR>.ast }

    method statement:sym<echo>($/) {
        make QAST::Op.new:
            :node($/),
            :op<print>,
            $<EXPR>.ast
    }


    method value:sym<string>($/)  { make $<quote_EXPR>.ast }
    method value:sym<integer>($/) { make QAST::IVal.new: :node($/), :value(+~$/) }
    method value:sym<number>($/)  { make QAST::NVal.new: :node($/), :value(+~$/) }


    method term:sym<value>($/) { make $<value>.ast }
    method term:sym<call>($/) {
        my $call := QAST::Op.new: :node($/), :op<call>, :name(~$<funcname>);
        for $<EXPR> {
            $call.push: $_.ast;
        }
        make $call;
    }
    method term:sym<var>($/) {
        my $name := ~$<varname>;
        my %sym := $*CUR-BLOCK.symbol($name);
        if $*MAYBE-DECL && !%sym<declared> {
            $*CUR-BLOCK.symbol($name, :declared);
            make QAST::Var.new: :$name, :scope<lexical>, :decl<var>;
        }
        else {
            make QAST::Var.new: :$name, :scope<lexical>;
        }
    }

    method term:sym<parenthesised>($/) { make $<EXPR>.ast }
}

class PHQ::Compiler is HLL::Compiler {
    method eval($code, *@_args, *%adverbs) {
        my $output := self.compile($code, :compunit_ok(1), |%adverbs);

        if %adverbs<target> eq '' {
            my $outer_ctx := %adverbs<outer_ctx>;
            $output := self.backend.compunit_mainline($output);
            if nqp::defined($outer_ctx) {
                nqp::forceouterctx($output, $outer_ctx);
            }

            $output := $output();
        }

        $output;
    }
}

sub MAIN(*@ARGS) {
    my $comp := PHQ::Compiler.new();
    $comp.language('phq');
    $comp.parsegrammar(PHQ::Grammar);
    $comp.parseactions(PHQ::Actions);
    $comp.command_line(@ARGS, :encoding<utf8>, :!rxtrace);
}

#:nnoremap <buffer> <Enter> :w<CR> \| :!%:p<CR>
# vim:ft=perl6 sw=4 ts=8 sts=4 et:
